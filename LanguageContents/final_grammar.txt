<program> -> <_records> <_procedures> <execute>
<execute> -> EXECUTE COLON <stmts> END
<_records> -> <records>
<_records> -> EPSILON
<records> -> RECORDS COLON <recs> ENDRECORDS
<recs> -> <rec> <_recs>
<_recs> -> <recs>
<_recs> -> EPSILON
<rec> -> REC ID COLON <decl_stmts> ENDREC
<_procedures> -> <procedures>
<_procedures> -> EPSILON
<decl_stmts> -> <decl_stmt> <_decl_stmts>
<_decl_stmts> -> <decl_stmts>
<_decl_stmts> -> EPSILON
<decl_stmt> -> <type> <id_list> SEMICOLON
<type> -> <basic_type> 
<type> -> STRING 
<type> -> MAT LT <basic_type> GT LSQ <dims> RSQ
<type> -> REC ID
<basic_type> -> INT
<basic_type> -> REAL
<basic_type> -> BOOL
<basic_type> -> CHAR
<id_list> -> ID <_id_list>
<_id_list> -> COMMA <id_list>
<_id_list> -> EPSILON
<dims> -> INT_LIT <_int_lit>
<_int_lit> -> COMMA INT_LIT
<_int_lit> -> EPSILON
<procedures> -> PROCS COLON <procs> ENDPROCS
<procs> -> <proc> <_procs>
<_procs> -> <procs>
<_procs> -> EPSILON
<proc> -> PROC ID LPAREN <param_list> RPAREN ARROW LPAREN <param_list> RPAREN COLON <stmts> ENDPROC
<param_list> -> <basic_type> ID <_param_list>
<_param_list> -> COMMA <param_list> 
<_param_list> -> EPSILON
<stmts> -> <stmt> <_stmts>
<_stmts> -> <stmts> 
<_stmts> -> EPSILON
<stmt> -> <decl_stmt> 
<stmt> -> <cond_stmt> 
<stmt> -> <loop_stmt> 
<stmt> -> <proc_call_stmt> 
<stmt> -> <assign_stmt> 
<stmt> -> <io_stmt> 
<stmt> -> <mat_assign_stmt> 
<stmt> -> <str_assign_stmt>
<cond_stmt> -> IF LPAREN <conds> RPAREN COLON <stmts> <else_if> <_cond_stmt>
<else_if> -> ELIF LPAREN <conds> RPAREN COLON <stmts> <else_if>
<else_if> -> EPSILON
<_cond_stmt> -> ELSE COLON <stmts> ENDIF
<_cond_stmt> -> ENDIF
<io_stmt> -> GET LPAREN <var> RPAREN SEMICOLON
<io_stmt> -> PUT LPAREN <put_param> RPAREN SEMICOLON
<put_param> -> <var>
<put_param> -> ENDL
<put_param> -> TAB
<put_param> -> STR_LIT 
<var> -> ID <_var>
<_var> -> LSQ <dims> RSQ
<_var> -> DOT ID
<_var> -> EPSILON
<proc_call_stmt> -> CALL ID LPAREN <proc_arg_list> RPAREN ARROW LPAREN <proc_arg_list> RPAREN SEMICOLON 
<proc_arg_list> -> <arg_list>
<proc_arg_list> -> EPSILON 
<arg_list> -> ID <arg_list1>
<arg_list1> -> COMMA <arg_list>
<arg_list1> -> LSQ <dims> RSQ <arg_list2>
<arg_list1> -> DOT ID <arg_list2>
<arg_list1> -> EPSILON
<arg_list2> -> COMMA <arg_list>
<arg_list2> -> EPSILON
<loop_stmt>  -> LOOP LPAREN <assign_list> RPAREN LPAREN <conds> RPAREN LPAREN <assign_list> RPAREN COLON <iloop_stmts> ENDLOOP
<iloop_stmts> -> <iloop_stmt> <iloop_stmts>
<iloop_stmts> -> EPSILON
<iloop_stmt> -> <stmt>
<iloop_stmt> -> BREAK SEMICOLON
<iloop_stmt> -> CONTINUE SEMICOLON
<assign_list> -> <assign_stmt> <assign_list>
<assign_list> -> EPSILON
<conds> -> LPAREN <conds> RPAREN <logical_op> LPAREN <conds> RPAREN
<conds> -> <elem> <rel_op> <elem>
<conds> -> NOT LPAREN <conds> RPAREN
<conds> -> BOOL_LIT
<logical_op> -> AND
<logical_op> -> OR
<rel_op> -> LT
<rel_op> -> GT
<rel_op> -> LE
<rel_op> -> GE
<rel_op> -> EQ
<rel_op> -> NEQ
<elem> -> <var>
<elem> -> INT_LIT
<elem> -> REAL_LIT
<elem> -> CHAR_LIT 
<assign_stmt> -> ASSIGN <var> ASSIGN_OP <expr> SEMICOLON
<expr> -> <term> <_expr>
<_expr> -> <op_sa> <term> <_expr>
<_expr> -> EPSILON
<term> -> <expo_term> <_term>
<_term> -> <op_mdp> <expo_term> <_term>
<_term> -> EPSILON
<expo_term> -> <factor> <_expo_term>
<_expo_term> -> <op_mm> <factor> <_expo_term>
<_expo_term> -> EPSILON
<factor> -> LPAREN <expr> RPAREN
<factor> -> <var>
<factor> -> <_const>
<factor> -> MINUS LPAREN <expr> RPAREN
<op_sa> -> PLUS 
<op_sa> -> MINUS
<op_mdp> -> MUL
<op_mdp> -> DIV
<op_mdp> -> MOD
<op_mm> -> POW
<mat_assign_stmt> -> MATASSIGN ID ASSIGN_OP <mat_stmt>
<mat_stmt> -> LCURL <row_list> RCURL SEMICOLON
<mat_stmt> -> ID <_mat_stmt> 
<_mat_stmt> -> PLUS ID SEMICOLON
<_mat_stmt> -> MAT_ROWS SEMICOLON
<_mat_stmt> -> MAT_COLS SEMICOLON
<row_list> -> <row> <_row_list>
<_row_list> -> SEMICOLON <row> <_row_list> 
<_row_list> -> EPSILON
<row> -> <_const> <_row>
<_row> -> COMMA <_const> <row>
<_row> -> EPSILON
<_const> -> INT_LIT
<_const> -> REAL_LIT
<_const> -> BOOL_LIT
<_const> -> CHAR_LIT
<str_assign_stmt> -> STRASSIGN ID ASSIGN_OP <str_stmt> 
<str_stmt> -> STR_LIT SEMICOLON
<str_stmt> -> ID <_str_stmt>
<_str_stmt> -> SEMICOLON
<_str_stmt> -> PLUS ID SEMICOLON
<_str_stmt> -> LSQ <ind> RSQ SEMICOLON
<_str_stmt> -> STR_LENGTH SEMICOLON 
<ind> -> ID
<ind> -> INT_LIT
